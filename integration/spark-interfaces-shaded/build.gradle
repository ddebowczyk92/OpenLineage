import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.diffplug.spotless' version '6.12.0'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "pmd"
}

ext {
    junit5Version = '5.10.1'
    assertjVersion = '3.25.3'
}


pmd {
    consoleOutput = true
    toolVersion = "6.46.0"
    rulesMinimumPriority = 5
    ruleSetFiles = rootProject.files("pmd-openlineage.xml")
    ruleSets = []
    ignoreFailures = true
}

pmdMain {
    dependsOn shadowJar
    reports {
        html.required = true
    }
}

pmdTest {
    dependsOn shadowJar
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    isReleaseVersion = !version.endsWith('SNAPSHOT')
}

dependencies {
    implementation("io.openlineage:openlineage-java:${project.version}") {
        exclude(group: 'io.micrometer')
        exclude(group: 'com.fasterxml.jackson.c ore')
        exclude(group: 'org.yaml')
        exclude(group: 'org.slf4j')
        exclude(group: 'commons-logging')
        exclude(group: 'commons-codec')
    }
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    testImplementation "org.junit.jupiter:junit-jupiter:${junit5Version}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
}

configurations {
    testImplementation.extendsFrom compileOnly
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

compileJava {
    options.incremental = true
    options.compilerArgs << '-parameters'
    options.encoding = "UTF-8"
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileTestJava {
    options.incremental = true
    options.compilerArgs << '-parameters'
    options.encoding = "UTF-8"
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

test {
    useJUnitPlatform()
}

spotless {
    def disallowWildcardImports = {
        String text = it
        def regex = ~/import .*\.\*;/
        def m = regex.matcher(text)
        if (m.find()) {
            throw new AssertionError("Wildcard imports disallowed - ${m.findAll()}")
        }
    }
    java {
        googleJavaFormat()
        removeUnusedImports()
        custom 'disallowWildcardImports', disallowWildcardImports
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.openlineage'
            artifactId = 'spark-interfaces-shaded'

            from components.java

            artifact sourceJar
            artifact javadocJar

            pom {
                name = 'spark-interfaces-shaded'
                description = 'OpenLineage Spark Java interfaces'
                url = 'https://github.com/OpenLineage/OpenLineage'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'openlineage'
                        name = 'OpenLineage Project'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/OpenLineage/OpenLineage.git'
                    developerConnection = 'scm:git:ssh://github.com:OpenLineage/OpenLineage.git'
                    url = 'https://github.com/OpenLineage/OpenLineage'
                }
            }

            pom.withXml {
                asNode().dependencies.'*'.findAll() {

                    it.groupId.text() == 'io.openlineage'
                    || it.groupId.text() == 'com.fasterxml.jackson.core'
                }.each() {
                    it.parent().remove(it)
                }
            }
        }
    }

    processResources {
        filter ReplaceTokens, tokens: [
                "version": project.property("version")
        ]
    }

    repositories {
        maven {
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username = System.getenv('RELEASE_USERNAME')
                password = System.getenv('RELEASE_PASSWORD')
            }
        }
    }
}

signing {
    required { isReleaseVersion }
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

archivesBaseName = "spark-interfaces-shaded"

shadowJar {
    dependsOn test

    dependencies {
        exclude 'org/slf4j/**'
        exclude 'org/yaml/**'
        exclude 'org/apache/**'
        exclude 'org/LatencyUtils/**'
        exclude 'org/HdrHistogram/**'
        exclude 'io/micrometer/**'
        exclude 'io/openlineage/client/OpenLineageClient**'
        exclude 'io/openlineage/client/OpenLineageYaml**'
        exclude 'io/openlineage/client/circuitBreaker/**'
        exclude 'io/openlineage/client/metrics/**'
        exclude 'io/openlineage/client/transports/**'
    }

    relocate('io.openlineage.client', 'io.openlineage.client.shaded')
    relocate('io.openlineage.server', 'io.openlineage.server.shaded')
    relocate('com.fasterxml', 'io.openlineage.com.fasterxml.shaded')

    archiveClassifier = ''
    // avoid conflict with any client version of that lib
    manifest {
        attributes(
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Built-By': System.getProperty('user.name'),
                'Build-Jdk': System.getProperty('java.version'),
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    zip64 true
}
